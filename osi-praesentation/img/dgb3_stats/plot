# Plot OpenSlides graphs (system)

set output "/tmp/a.svg"

# time frame
# now = time(0)
# set xrange [now - 24*60*60 : now]
#
start_of_congress = 1526076000
end_of_congress   = 1526562000
set xrange [start_of_congress : end_of_congress]

# screen
# set terminal svg size 1280, 768
# set multiplot layout 2,2
# vertical
set terminal svg size 800,1280
set multiplot layout 5,1

set grid
set lmargin 10
set style data lines
set colorsequence podo

set xdata time
set timefmt "%s"
set format x "%m/%d %H:%M"

set xtics 10800
set xtics rotate by 60 right

# Nginx Connections
set title "Nginx connections"
set ylabel "No. of connections"
set autoscale y
set offset graph 0, 0, 20, 0
# set style data filledcurves x
plot "cons.dat" using 1:2 title "active", \
             "" using 1:3 title "writing", \
             "" using 1:4 title "waiting"

# Redis keys
set title "Redis keys"
set ylabel "Number of keys"
set offset graph 0, 0, 10, 0
plot "redis-keys.dat" using 1:2 title "restricted"

# Load
set title "System load"
set ylabel "Load"
set autoscale y
set offset graph 0, 0, 0.2, 0
plot "atop.dat" using 1:2 title "1 min", \
             "" using 1:3 title "5 min", \
             "" using 1:4 title "15 min"

# Mem
set title "Memory usage"
# avail_ram = 64
# set yrange[0:avail_ram]
set offset graph 0, 0, 5, 0
set ylabel "GB"
plot "atop.dat" using 1:5 title "RAM", \
             "" using 1:6 title "Swap"

# vnstat
set title "Network traffic"
set ylabel "MB"
set offset graph 0, 0, 0, 0
plot "vnstat.dat" using 1:2 title "RX", \
               "" using 1:3 title "TX"

# vim: ft=gnuplot :
